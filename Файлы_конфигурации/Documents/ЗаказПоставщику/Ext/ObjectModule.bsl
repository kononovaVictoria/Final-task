//Вычисление СуммыДокумента перед записью
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	//Чтобы нельзя было поправить сумму документа перед записью документа
	СуммаДокумента=Таблица.Итог("Сумма");
	
КонецПроцедуры

// Движение в регистр ЗаказыПоставщикам Приход
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ЗаказыПоставщикам Приход
	Движения.ЗаказыПоставщикам.Записывать = Истина;
	Для Каждого ТекСтрокаТаблица Из Таблица Цикл
		Движение                          = Движения.ЗаказыПоставщикам.Добавить();
		Движение.ВидДвижения              = ВидДвиженияНакопления.Приход;
		Движение.Период                   = Дата;
		Движение.Организация              = Организация;
		Движение.Контрагент               = Контрагент;
		Движение.Договор                  = Договор;
		Движение.ЗаказПоставщику          = Ссылка; //ЭтотОбъект;
		Движение.Номенклатура             = ТекСтрокаТаблица.Номенклатура;
		Движение.ВариантНоменклатуры      = ТекСтрокаТаблица.НаборСвойств;
		Движение.Количество               = ТекСтрокаТаблица.Количество*ТекСтрокаТаблица.КоэффициентЕдиницыИзмерения;
		Движение.Сумма                    = ТекСтрокаТаблица.Сумма;
	КонецЦикла;

	
КонецПроцедуры

//Ввод на основании справочника Контрагенты и Номенклатура, ВариантНоменклатуры
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда 
		
		Если ДанныеЗаполнения.ЭтоГруппа Тогда
			
			Сообщение       = Новый СообщениеПользователю();
			Сообщение.Текст = "Поле  <<Контрагент>> не заполнено. Была выбрана Группа <<" + ДанныеЗаполнения + ">>, а не конкретный продавец!";
			Сообщение.Поле  = "Контрагент";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			
		ИначеЕсли ДанныеЗаполнения.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель Тогда	
			
			Сообщение       = Новый СообщениеПользователю();
			Сообщение.Текст = "Поле  <<Контрагент>> не заполнено. Выбранный контрагент <<" + ДанныеЗаполнения + ">> является Покупателем, а не Продавцом!";
			Сообщение.Поле  = "Контрагент";
			Сообщение.УстановитьДанные(ЭтотОбъект);                                                                            
			Сообщение.Сообщить();
			
		Иначе
			
			// Заполнение шапки
			Контрагент      = ДанныеЗаполнения;
			ОбъектОснование = ДанныеЗаполнения;
			
			Сообщение       = Новый СообщениеПользователю();
			Сообщение.Текст = "Поле  <<Контрагент>> заполнено: <<" + ДанныеЗаполнения + ">> ";
			Сообщение.Поле  = "Контрагент";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		Если ДанныеЗаполнения.ЭтоГруппа Тогда
			
			Сообщение       = Новый СообщениеПользователю();
			Сообщение.Текст = "Колонка <<Номенклатура>> не заполнена. Была выбрана Группа <<" + ДанныеЗаполнения + ">>, а не конкретная номенклатура!";
			Сообщение.Поле  = "Таблица.Номенклатура";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		Иначе
			
			// Заполнение шапки
			Контрагент               = ДанныеЗаполнения.Поставщик;
			ОбъектОснование          = ДанныеЗаполнения;
			
			НоваяСтрока              = Таблица.Добавить();
			НоваяСтрока.Номенклатура = ДанныеЗаполнения;
			
			Сообщение                = Новый СообщениеПользователю();
			Сообщение.Текст          = "Колонка <<Номенклатура>> заполнена. Была выбрана номенклатура <<" + ДанныеЗаполнения + ">>.";
			Сообщение.Поле           = "Таблица[0].Номенклатура";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			
			
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ВариантыНоменклатуры") Тогда
		
		// Заполнение шапки
		Контрагент               = ДанныеЗаполнения.Владелец.Поставщик;
		ОбъектОснование          = ДанныеЗаполнения;
		
		НоваяСтрока              = Таблица.Добавить();
		НоваяСтрока.Номенклатура = ДанныеЗаполнения.Владелец;
		НоваяСтрока.НаборСвойств = ДанныеЗаполнения;
		
		Сообщение                = Новый СообщениеПользователю();
		Сообщение.Текст          = "Колонки <<Номенклатура>> и <<НаборСвойств>> заполнены:  <<" + ДанныеЗаполнения.Владелец + ">> и <<" + ДанныеЗаполнения + ">>.";
		Сообщение.Поле           = "Таблица[0].Номенклатура";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры


